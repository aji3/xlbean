buildscript {
    repositories {
        mavenCentral()
    }
}
plugins {
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.1'
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'org.xlbean'
archivesBaseName = 'xlbean'
version = '0.1.4'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

bintray {
    user = hasProperty('BINTRAY_USER') ? getProperty('BINTRAY_USER') : System.getenv('BINTRAY_USER')
    key = hasProperty('BINTRAY_API_KEY') ? getProperty('BINTRAY_API_KEY') : System.getenv('BINTRAY_API_KEY')
    pkg {
        repo = 'xlbean'
        name = 'xlbean'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/aji3/xlbean.git'
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = 'xlbean ' + project.version
            released  = new Date()
            vcsTag = project.version
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
	        gpg {
	            sign = true
	            passphrase = hasProperty('BINTRAY_GPG_KEY') ? getProperty('BINTRAY_GPG_KEY') : System.getenv('BINTRAY_GPG_KEY')
	        }
        }
    }
    publications = ['mavenPublication']
    configurations = ['archives']
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
        }
    }
}

repositories {
   mavenCentral()
}

dependencies {
    compile 'org.apache.poi:poi:3.16'
    compile 'org.apache.poi:poi-ooxml:3.16'
    compile 'org.slf4j:slf4j-api:1.7.25'

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'ch.qos.logback:logback-classic:1.2.2'
    testCompile 'ch.qos.logback:logback-core:1.2.2'
}

jacoco {
    toolVersion = "0.7.6.201602180812"
}

javadoc {
    failOnError = false
    destinationDir = file('docs')
    options.locale = 'en_US'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
